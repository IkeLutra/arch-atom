# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=atom
pkgver=1.9.9
pkgrel=3
pkgdesc='A hackable text editor for the 21st Century'
arch=('i686' 'x86_64')
url='https://github.com/atom/atom'
license=('MIT' 'custom')
depends=('apm'
         'electron')
makedepends=('git' 'npm')
optdepends=('ctags: symbol indexing support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/atom/atom/archive/v${pkgver}.tar.gz"
        'autocomplete-plus.patch'
        'beforeunload.patch'
        'deprecated-api.patch'
        'fix-atom-sh.patch'
        'fix-license-path.patch'
        'fix-marker-index.patch'
        'fix-oniguruma.patch'
        'fix-pane-resize-handle.patch'
        'run-as-node.patch'
        'settings-view-fix-height.patch'
        'symbols-view-use-system-ctags.patch'
        'tree-view.patch'
        'use-system-apm.patch'
        'use-system-electron.patch')
sha256sums=('fc5305d1e686f0fd5a56ba2f1938b01c3a85c67cdab9b7f3dcdf2586f28f231a'
            '58198d214edc434580c5764f749e26fb686d77c3c983d01f247f48aec84eb1a4'
            'e92e23bbf839bec6611b2ac76c1f5bba35b476983b0faa9b310288e2956247a2'
            '6fca91b3e80248a96fc4b6b0228602d4dd68ef851cb059a97a7379e72e53b432'
            'd3eb239f53feb3181fb4d57d2db391d7b31ce2188c5bd5b79d9ed3133351cd90'
            'ffd67934c3f80b87dfe7706191c9afad79289b6640b2876f1439701d6827a759'
            '2569702bee0508ee38a181f28bc99d6844725de206362a010705d84da09735c3'
            'c0ae6c64802bc20219bc9142bd6e62e65853044b4a678d3f7ec72dcb61d22274'
            '41aaa35b638e29d28b5d92077b7b242725046cc11d4c51d3eca589eaed977ec5'
            '1533581eeede375924a3431fed7e6641a156f765bf252590d73da4259f07343f'
            'abafa58a3a9c81b7ecaf0d5ac001b0a164d016db4521fab9ad954f4ccbc8c6dc'
            'a5a2b1fefa29799b3225c4aa3fe0e5b057b2b320f98ad7bc28d3596923e52b41'
            '109af01ceae45615994f2ac08292c44db339bf7ef89369b077e04fcfc35aa3dd'
            '7771ff6ddf06bedeb37ff47bf98f685cc67585e6f831d2add9a31078383a6fbe'
            '23d9d2976ad23f17d7c6e7e09c5de5aceae72a5577f811723bf8c7885b319c05')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}"/fix-atom-sh.patch
  patch -Np1 -i "${srcdir}"/use-system-electron.patch
  patch -Np1 -i "${srcdir}"/use-system-apm.patch
  patch -Np1 -i "${srcdir}"/fix-license-path.patch

  # apm with system (updated) nodejs cannot 'require' modules inside asar
  sed -e "s/, 'generate-asar'//" -i build/Gruntfile.coffee

  # Fix for Electron 1.2.0
  patch -Np1 -i "${srcdir}"/beforeunload.patch
  patch -Np1 -i "${srcdir}"/run-as-node.patch

  # Fix for Electron 1.3.0
  patch -Np1 -i "${srcdir}"/fix-pane-resize-handle.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export ATOM_RESOURCE_PATH="$srcdir/atom-$pkgver"
  # If unset, ~/.atom/.node-gyp/.atom/.npm is used
  export NPM_CONFIG_CACHE="${HOME}/.atom/.npm"
  apm clean
  apm install

  # Fix height of "Choose a Theme" and "Install Packages" panels
  cd node_modules/settings-view
  patch -Np1 -i "${srcdir}"/settings-view-fix-height.patch
  cd ../..

  # Use system ctags
  cd node_modules/symbols-view
  patch -Np1 -i "${srcdir}"/symbols-view-use-system-ctags.patch
  rm -r vendor
  cd ../..

  # Fix for Node 6
  cd node_modules/autocomplete-plus
  patch -Np1 -i "${srcdir}"/autocomplete-plus.patch
  cd ../..
  cd node_modules/tree-view
  patch -Np1 -i "${srcdir}"/tree-view.patch
  cd ../..

  # Fix for Electron 1.3
  _node_gyp="node-gyp rebuild --target=$(</usr/lib/electron/version) --dist-url=https://atom.io/download/atom-shell"
  cd node_modules/marker-index
  patch -Np1 -i "${srcdir}"/fix-marker-index.patch
  ${_node_gyp}
  cd ../oniguruma
  patch -Np1 -i "${srcdir}"/fix-oniguruma.patch
  ${_node_gyp}
  cd ../../

  _packagesToDedupe=('abbrev'
                     'amdefine'
                     'atom-space-pen-views'
                     'cheerio'
                     'domelementtype'
                     'fs-plus'
                     'grim'
                     'highlights'
                     'humanize-plus'
                     'iconv-lite'
                     'inherits'
                     'loophole'
                     'oniguruma'
                     'q'
                     'request'
                     'rimraf'
                     'roaster'
                     'season'
                     'sigmund'
                     'semver'
                     'through'
                     'temp')
  apm dedupe ${_packagesToDedupe[@]}
  cd build
  npm install
  cd ..
  script/grunt --channel=stable
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d -m 755 "${pkgdir}"/usr/lib
  cp -r out/Atom/resources/app "${pkgdir}"/usr/lib
  mv "${pkgdir}"/usr/lib/app "${pkgdir}"/usr/lib/atom

  install -d -m 755 "${pkgdir}/usr/share/applications"
  sed -e "s|<%= appName %>|Atom|" \
      -e "s/<%= description %>/${pkgdesc}/" \
      -e "s|<%= installDir %>/share/<%= appFileName %>/atom|electron --app=/usr/lib/atom|" \
      -e "s|<%= iconPath %>|/usr/lib/atom/resources/atom.png|" \
      resources/linux/atom.desktop.in > "${pkgdir}/usr/share/applications/atom.desktop"

  install -D -m 755 out/Atom/resources/new-app/atom.sh "${pkgdir}/usr/bin/atom"

  install -D -m 644 out/Atom/resources/LICENSE.md \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"

  # Remove useless stuff
  find "${pkgdir}"/usr/lib/atom/node_modules \
      -name "*.a" -exec rm '{}' \; \
      -or -name "*.bat" -exec rm '{}' \; \
      -or -name "benchmark" -prune -exec rm -r '{}' \; \
      -or -name "doc" -prune -exec rm -r '{}' \; \
      -or -name "html" -prune -exec rm -r '{}' \; \
      -or -name "man" -prune -exec rm -r '{}' \; \
      -or -path "*/less/gradle" -prune -exec rm -r '{}' \; \
      -or -path "*/task-lists/src" -prune -exec rm -r '{}' \;
}
